buildscript {
    ext {
        kotlin_version = "1.3.50"
    }
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "pl.allegro.tech.build:axion-release-plugin:1.10.2"
    }
}

apply plugin: "pl.allegro.tech.build.axion-release"

scmVersion {
    tag {
        prefix = ""
    }
}

allprojects {
    repositories {
        mavenCentral()
        jcenter()
    }
}

configurations {
    ktlint
}

dependencies {
    ktlint "com.github.shyiko:ktlint:0.31.0"
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "**/src/**/*.kt"
}

task check(group: "verification", dependsOn: ktlint)

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "-F", "**/src/**/*.kt"
}
